module camera;

import std.math, opengl;

interface Camera {
  void gl-setup();
}

class PerspectiveCam : Camera {
  float fov, zNear, zFar, aspect;
  alias args = (fov, aspect, zNear, zFar);
  void init() {
    args = (45f, 1f, 0.01f, 100f);
  }
  void gl-setup() mode GL {
    MatrixMode PROJECTION;
    LoadIdentity;
    gluPerspective args;
  }
}

template WorldCam(T) {
  class WorldCam : T {
    vec3f up, pos, lookat;
    alias dir = lookat - pos;
    vec3f setDir(vec3f v) { lookat = pos + v; return lookat; }
    void init() {
      super.init();
      (up, pos) = (vec3f.Y, vec3f(0));
      setDir -vec3f.Z;
    }
    void gl-setup() mode GL {
      super.gl-setup();
      MatrixMode MODELVIEW;
      LoadIdentity;
      auto dirz = dir;
      dirz.z = -dirz.z;
      auto
        left = up.cross3f(dirz).normalized,
        up = dirz.cross3f(left).normalized;
      (vec3f.Y.angle3f(up, left) / PI180).Rotatef vec3f.X;
      (vec3f.X.angle3f(left, up) / PI180).Rotatef vec3f.Y;
      Translatef (-pos);
    }
  }
}

vec3f normalize3f(vec3f v) return v.normalized;

template EgoCam(T) {
  class EgoCam : T {
    vec3f pos;
    float turnX, turnY;
    void init(vec3f p, float x, y) { (pos, turnX, turnY) = (p, x, y); super.init(); }
    void init() { init(vec3f(0), 0, 0); }
    void turn-left(float f) { turnX += f; }
    alias lowlimit = -PI / 2 + 0.1;
    alias highlimit = PI / 2 - 0.1;
    void turn-up(float f) { turnY -= f; if (turnY < lowlimit) turnY = lowlimit; if (turnY > highlimit) turnY = highlimit; }
    alias dir = vec3f.Z.rotate3f(vec3f.X, turnY).rotate3f(vec3f.Y, turnX).normalize3f;
    alias left = vec3f.Y.cross3f(dir).normalize3f;
    void gl-setup() mode GL {
      super.gl-setup();
      MatrixMode MODELVIEW;
      LoadIdentity;
      auto dirz = dir; dirz.z = -dirz.z;
      auto left = vec3f.Y.cross3f(dirz).normalized, up = dirz.cross3f(left).normalized;
      (vec3f.Y.angle3f(up, left) / PI180).Rotatef vec3f.X;
      (vec3f.X.angle3f(left, up) / PI180).Rotatef vec3f.Y;
      Translatef (-pos);
    }
  }
}
