module std.coroutine; // coroutines. TODO: make play nice with boehm by explicitly registering stack on leaving

import std.file, c.ucontext, c.sys.mman;

extern(C) void makecontext(ucontext_t* ucp, void* func, int argc, ...);

extern(C) void call(void delegate() dg, (void*, type-of mem)* newdata) {
  auto _threadlocal = (*newdata)[0];
  mem = (*newdata)[1];
  dupvcache = null;
  set-handler (UnrecoverableError err) {
    import std.process;
    writeln "$(read-pretty-fcc-backtrace())\n!\t$err";
    _interrupt 3;
  }
  dg();
}

class Coroutine {
  void[] stack;
  ucontext_t ctx;
  void delegate() leave;
  void delegate(void delegate() yield) task;
  void exit() { leave(); }
  bool complete;
  UnrecoverableError err;
  void run() {
    set-handler(UnrecoverableError uerr) {
      this.err = uerr;
      invoke-exit "exit";
    }
    define-exit "exit" exit();
    task(&exit);
    complete = true;
    exit();
    raise new Error "Coroutine invoked but task has returned. ";
  }
  void mapStack(int stacksize, bool dontswap = false) {
    int flags = MAP_ANONYMOUS|MAP_PRIVATE|MAP_STACK|MAP_NORESERVE;
    // see https://lists.ozlabs.org/pipermail/linuxppc-dev/2015-May/128539.html
    alias MAP_LOCKONFAULT = MAP_HUGETLB << 1;
    if (dontswap) flags |= MAP_LOCKONFAULT;
    auto ptr = mmap(null, stacksize, PROT_READ|PROT_WRITE, flags, -1, 0);
    if (!ptr) raise new ErrnoError;
    stack = ptr[0..stacksize];
  }
  void init() { } // careful with this
  void init(void delegate(void delegate() yield) task, int stacksize = 1024*1024, bool dontswap = false) {
    this.task = task;
    assert(!!task);
    if (getcontext(&ctx) == -1) raise new ErrnoError;
    mapStack(stacksize, dontswap => dontswap);
    ctx.uc_stack.(ss_sp, ss_size, ss_flags) = (stack.ptr, stacksize, 0);
    auto newdata = new type-of (copy_tls(), mem);
    (*newdata) = (copy_tls(), mem);
    makecontext(&ctx, void*: &call, size-of (void delegate(), void*)/size-of int, &run, newdata);
  }
  void free() {
    munmap stack.(ptr, length);
    super.free;
  }
  void enter() {
    ucontext_t prev;
    leave = \{
      if (swapcontext(&ctx, &prev) == -1) raise new ErrnoError;
    }
    if (swapcontext(&prev, &ctx) == -1) raise new ErrnoError;
    if (err) { auto takeErr = err; err = null; raise new Error "[--coroutine--]\n$takeErr"; }
  }
}

template setupCoIterator(T) {
  Iterator!T setupCoIterator(void delegate(void delegate(T)) dg) {
    ref res = * T*: mem.malloc size-of T, done = *new bool;
    auto coro = new Coroutine new \(void delegate() yield) {
      dg \(T t) { res = t; yield(); };
      done = true;
      yield(); fail;
    };
    return new class : Iterator!T {
      T* rp; bool* dp; Coroutine coro;
      alias res = *rp; alias done = *dp;
      void init(T* rp, bool* dp, Coroutine coro) { this.(rp, dp, coro) = (rp, dp, coro); }
      bool advance() {
        if (done) return false;
        coro.enter();
        if (done) { coro.free; return false; }
        value = res;
        return true;
      }
    } (&res, &done, coro);
  }
}

void delegate() simpleCoro(void delegate(void delegate() yield) task) {
  auto coro = new Coroutine task;
  return &coro.enter;
}

template feed(T) {
  void delegate(T) feed(void delegate(T delegate() yield) dg) {
    ref data = (new (T, ()))._0;
    auto coro = new Coroutine new λ(void delegate() yield) {
      dg λ{
        yield();
        return data;
      }
      yield(); fail;
    };
    coro.enter; // prime
    return new λ(T t) {
      data = t;
      coro.enter;
    }
  }
}
