/* Title: std.http
Simple HTTP processing */
module std.http;

import std.string, std.util, std.socket;

class DownloadError : Error {
  void init(string url, string msg) { super.init "While downloading '$url': $msg"; }
}

/* Function: download
*Deprecated!*
Download a site. HTTP/1.0, very WIP.

Parameters:
url - URL
onRedirect - Called when the request is redirected */
ubyte[] download(string url, void delegate(string) onRedirect = null, int timeout = 0, int* responsecode = null) {
  auto original-url = url;
  {
    string rest = url.startsWith("http://");
    if !rest raise new DownloadError(url, "Unknown protocol (forgot http://?)");
    url = rest;
  }
  (string domain, url) = slice (url, "/");
  url = "/" ~ url;
  (domain, string portstr) = slice (domain, ":");
  short port = 80;
  if portstr.length port = short:portstr.atoi();
  auto sock = new Socket(domain, port);
  if (timeout) sock.setTimeout(timeout);
  void sendLine(string line) {
    sock.sendAll ubyte[]:line;
    sock.sendAll ubyte[]:"\r\n";
  }
  sendLine "GET $url HTTP/1.0";
  sendLine "Host: $domain";
  sendLine "";
  auto response = string: join readsocket sock;
  onSuccess[response] response.free;
  string headerstr = slice(&response, "\r\n\r\n");
  if (headerstr.find("Location: ")) {
    auto url2 = original-url.followLink headerstr.between("Location: ", "\r\n");
    if (onRedirect) { onRedirect url2.dup; }
    return download (url2, onRedirect, timeout, responsecode);
  }
  // careful: response will be freed on function end
  auto header = headerstr.split "\r\n";
  // HTTP/1.1 200 OK
  string (proto, code_str, message) = split(header[0], " ")[(0, 1, 2)];
  if (responsecode) *responsecode = atoi code_str;
  
  return ubyte[]:response.dup;
}

(ubyte[], string)[auto~] cache;
import std.thread;
shared Mutex cachelock;

void init() cachelock = new Mutex;

/* Function: downloadCached
*Deprecated!*
Like <download>, but cached. Self-explanatory. */
ubyte[] downloadCached(string url, void delegate(string) onRedirect = null, int timeout = 0) {
  using LockedMutex:cachelock {
    for auto tup <- cache
      if tup[1] == url return tup[0];
  }
  
  auto res = download (url, onRedirect, timeout);
  using LockedMutex:cachelock
    cache ~= (res, url);
  return res;
}

/*Function: followLink
Returns the combined URL of the two parameters. For instance, if *from* is "google.de/webhp" and *to* is "/search", the result is "google.de/search".*/
reassign string followLink(string from, string to) {
  // absolute link
  if (to.find("://")) return to;
  string schema = from.between("", "://");
  if (!schema) raise new Error "'$from' is not a valid URL! ";
  from = from.between("://", "");
  if to.startsWith "//"
    return schema ~ ":" ~ to;
  if to.startsWith "/"
    return "$schema://" ~ from.between("", "/") ~ to;
  if to.startsWith "?"
    return "$schema://" ~ from.between("", "?") ~ to;
  if (!from.find "/") raise new Error "no separator slash in \"$from\"";
  return "$schema://" ~ from.between("", "/", =>reverse-search, =>reverse-order) ~ "/" ~ to;
}

/*Function: urlEncode
Encodes a string using percent encoding for use in HTTP. */
string urlEncode(string data, string extra_safe = "+-_.~") {
  char[auto~] encoded;
  for char ch <- data {
    if (("A" <= ch <= "Z") || ("a" <= ch <= "z") || ("0" <= ch <= "9") || extra_safe.find((&ch)[0..1])) {
      encoded ~= ch;
    } else {
      scope a = std.string.toString(ch, 16);
      scope string b;
      if (a.length < 2) b = "0$a";
      else b = a.dup;
      encoded ~= "%";
      encoded ~= b;
    }
  }
  return encoded[];
}

/*Function: pathEncode
Does exactly what <urlEncode> does, except / is a safe character. */
string pathEncode(string uri) {
  return urlEncode(uri, "-_.~/");
}

/*Function:urlDecode
Inverse of urlEncode. But also: "+" is treated as space! */
string urlDecode(string data) {
  char[auto~] decoded;
  auto iter = data.iterator;
  for char ch <- iter {
    if (ch == "+") decoded ~= " ";
    else if (ch != "%") decoded ~= ch;
    else {
      bool success = (char a <- iter) && (char b <- iter);
      if (!success) fail "two characters expected after %";
      int hexdecode(char ch) {
        if ("0" <= ch <= "9") return ch - "0";
        if ("a" <= ch <= "f") return ch - "a" + 10;
        if ("A" <= ch <= "F") return ch - "A" + 10;
        fail "character '$ch' is not hex";
      }
      int i = hexdecode a << 4 | hexdecode b;
      decoded ~= char:ubyte:i;
    }
  }
  return decoded[];
}

/*Function: createURI
Assembles a URI from the basic parts, used by <HTTP-URI>'s <toString> function.*/
string createURI(string host, int port=80, string path="/", (string,string)[] get=null, bool ssl=false) {
  string uri;
  if ssl
    uri="https://";
  else
    uri="http://";
  uri="$uri$host";
  if port!=80
    uri="$uri:$port";
  if path.startsWith "/"
    uri="$uri$(pathEncode(path))";
  else
    uri="$uri/$(pathEncode(path))";
  if get.length>0
    uri="$uri?";
  for auto x <- get {
    if uri[$-1]!="?"
      uri="$uri&";
    uri="$uri$(urlEncode(x[0]))=$(urlEncode(x[1]))";
  }
  return uri;
}

/*Struct: HTTP-URI
Represents an easily accessible/modifiable HTTP URL.*/
struct HTTP-URI {
  /*Variables: HTTP-URI
  
  ssl   - whether or not to use TLS encryption (https vs http)
  host  - the host to connect to (google.com)
  port  - the port to connect to (default = 80)
  path  - the path to use in a request (default = /)
  get   - contains all the variables in a GET request (stuff after ?)*/
  bool ssl;
  string host;
  int port;
  string path;
  (string,string)[] get;
}

/*Function: decodeURI
Takes a string URL as its input and returns a pointer to an appropriate HTTP-URI object.*/
void decodeURI(string url, HTTP-URI* uri) {
  alias obj = *uri;
  //protocol
  auto rest=url.startsWith "http://";
  if rest
    obj.ssl=false;
  else {
    rest=url.startsWith "https://";
    if rest
      obj.ssl=true;
    else
      raise new DownloadError(url,"Could not decode URI: Protocol is not HTTP");
  }
  
  //host
  string (domain, path) = rest.slice "/";
  (obj.host,string port) = domain.slice ":";
  obj.port = atoi(port);
  if !obj.port
    if obj.ssl
      obj.port=443;
    else
      obj.port=80;
  
  //path
  (obj.path,string get) = path.slice "?";
  
  obj.path = "/" ~ obj.path;
  
  for auto kv <- once(get).splitAt "&" {
    (string,string) p=kv.slice "=";
    obj.get ~= (p[0].dup,p[1].dup);
  }
}

string toString(HTTP-URI u) {
  return createURI u.(host, port, path, get, ssl);
}

/* Function: sendRequest
Sends an HTTP/1.1 request to the server specified in the url. 
Parameters:
  url     - required, the URL of the resource.
  method  - optional, the method to use. Default: GET
  header  - optional, any header data. Host and content-length will be automatically handled
  msgbody - optional, the body of the request.
Returns:
  A tuple containing the status code, the header (an array of tuples of strings, (key,value)[]), and the body (ubyte[]). A status code of 0 means that there was an internal error with the function.
TODO:
  Make it clear spaces from fields in the headers.*/
int sendRequest(HTTP-URI url, string method = "GET",
  (string, string)[] header_in = null, (string, string)[]* header_out = null,
  ubyte[] body_in = null, ubyte[]* body_out = null) {

  // whine that there's no ssl support
  if url.ssl {
    raise new DownloadError(url.toString(),"No SSL support currently! :C");
    return 0;
  }
  
  // connect to server
  auto sock = new Socket(url.host,short:url.port);
  
  void sendLine(string line) {
    sock.sendAll ubyte[]:line;
    sock.sendAll ubyte[]:"\r\n";
  }
  
  // send request
  sendLine "$method $(url.path) HTTP/1.1";
  sendLine "Host: $(url.host)";
  sendLine "Connection: close";
  if body_in.length
    sendLine "Content-Length: $(body_in.length)";
  for auto l <- header_in
    sendLine "$(l[0]): $(l[1])";
  sendLine ""; //empty line separating header/body
  
  sock.sendAll body_in;
  
  // read response
  auto response = string: join readsocket sock;
  string (reshead, resbody) = response.slice "\r\n\r\n"; // splits header from body at the empty line separating them
  
  // process header
  //string[] head_split = (once ((once reshead).splitAt "\r\n "#.join)).splitAt "\r\n"; // splits the header into individual lines
  string[] headsplit = (reshead.split("\r\n ").join()).split "\r\n";
  // removes separation of lines starting with " " as per standard: http://www.w3.org/Protocols/rfc2616/rfc2616-sec4.html#sec4.2
  (string statusline, headsplit) = headsplit[(0, 1..$)]; // pops the first element into statusline
  // Status-Line = HTTP-Version SP Status-Code SP Reason-Phrase CRLF
  // http://www.w3.org/Protocols/rfc2616/rfc2616-sec6.html#sec6
  // ex: HTTP/1.1 200 OK
  int status = atoi ( statusline.split(" ")[1] ); //splits it into components and grabs the thing after the HTTP version (the status code)
  (string, string)[auto~] head;
  // whitespace removal goes here:
  for auto l <- headsplit {
    head ~= l.dup.slice ":" #.(that[0].strip(), that[1].strip());
  }
  
  // no body processing is done
  
  // close connection
  // TODO: Support Keep-Alive
  sock.close();
  
  // return parsed response
  if (header_out) *header_out = head[];
  if (body_out) *body_out = ubyte[]: resbody;
  
  return status;
}
