/*Title: std.socket
Wraps the BSD Sockets API*/
module std.socket;

import std.string, std.math, std.async, c.unistd, c.errno;
static import std.file;
platform(*-mingw32) {
  pragma(define, "_WIN32_WINNT=0x0501"); // target xp
  import c.winsock2, c.ws2tcpip;
  pragma(lib, "ws2_32");
  void init() { WSAStartup(2<<8 | 2, &WSADATA bogus); }
  
  alias c_socket_linger = c.winsock2.linger;
  
  extern(Windows) DWORD FormatMessageA(DWORD,PCVOID,DWORD,DWORD,char*,DWORD,void*);
  // inline in the system header, seriously?
  char* gai_strerrorA(int ecode) {
    import c.winbase;
    auto buffer = new char[] (1024 + 1);
    DWORD dwFlags = FORMAT_MESSAGE_FROM_SYSTEM
                  | FORMAT_MESSAGE_IGNORE_INSERTS
                  | FORMAT_MESSAGE_MAX_WIDTH_MASK;
    DWORD dwLanguageId = (SUBLANG_DEFAULT << 10) | LANG_NEUTRAL;
    FormatMessageA(dwFlags, null, ecode, dwLanguageId, buffer.ptr, 1024, null);
    return buffer.ptr;
  }
  alias gai_strerror = gai_strerrorA;
  alias EWOULDBLOCK = WSAEWOULDBLOCK;
}
platform(posix) {
  import c.sys.socket, c.netdb, c.string;
  alias c_socket_linger = c.sys.socket.linger;
}

/*Class: Address
An internet address*/
class Address {
  ubyte[] saddr;
  (sockaddr*, int) getAddrHandle() {
    return (sockaddr*:saddr.ptr, saddr.length);
  }
  int family() { return (sockaddr_in*:saddr.ptr).sin_family; }
  void init() { }
  /*Function: init
  Create an Address from host name and port*/
  void init(string dns, short port, bool allow-ipv6 = true) {
    addrinfo* addr;
    getaddrinfo(toStringz dns, null, null, &addr);
    if (!addr) {
      fail "Could not resolve $dns";
    }
    while addr {
      if (addr.ai_family == AF_INET) {
        int required_size = max(size-of sockaddr_in, addr.ai_addrlen);
        saddr = new ubyte[] required_size;
        saddr[0..addr.ai_addrlen] = (ubyte*:addr.ai_addr)[0..addr.ai_addrlen];
        (sockaddr_in*:saddr.ptr).sin_family = AF_INET;
        (sockaddr_in*:saddr.ptr).sin_port = htons(port);
        return;
      }
      if (addr.ai_family == AF_INET6 && allow-ipv6) {
        int required_size = max(size-of sockaddr_in6, addr.ai_addrlen);
        saddr = new ubyte[] required_size;
        saddr[0..addr.ai_addrlen] = (ubyte*:addr.ai_addr)[0..addr.ai_addrlen];
        (sockaddr_in*:saddr.ptr).sin_family = AF_INET6;
        (sockaddr_in*:saddr.ptr).sin_port = htons(port);
        return;
      }
      addr = addr.ai_next;
    }
    fail "No IP address found for $dns";
  }
  Address dup() {
    auto res = new Address;
    res.saddr = saddr.dup;
    return res;
  }
  string asString() {
    auto required = max(INET_ADDRSTRLEN, INET6_ADDRSTRLEN) + 1;
    scope buffer = new char[] required;
    auto res = getnameinfo(sockaddr*:saddr.ptr, saddr.length, buffer.(ptr, length), null, 0, NI_NUMERICHOST);
    if (res != 0) fail CToString gai_strerror(res);
    return CToString(buffer.ptr).dup;
  }
}

alias TcpAddress = Address; // TODO implement "alias deprecated"

class SocketError : Error {
  void init(int i) { super.init("socket operation failed: $i"); }
  void init(string s) { super.init "socket operation failed: $s"; }
}

alias INVALID_SOCKET = -1;

/*Class: Socket
A BSD Socket*/
class Socket : AsyncHandle {
  int sockfd;
  override int[] getFds() { return (&sockfd)[0..1]; }
  Address boundAddr;
  /*Function: close*/
  void close() {
    if (sockfd == INVALID_SOCKET) return;
    auto err = c.unistd.close(sockfd);
    if (err == -1) {
      raise new Error "While closing socket: $(CToString strerror errno)";
    }
    sockfd = INVALID_SOCKET;
  }
  void free() { boundAddr?.free; super.free; }
  /*Function: init*/
  void init(bool bare = false, int family = AF_INET) {
    if (!bare) {
      sockfd = socket (family, SOCK_STREAM, 0);
      linger(true, 30); // default: linger 30s
    }
  }
  /*Function: init*/
  void init(string domain, short port, bool allow-ipv6 = true) {
    scope addr = new Address(domain, port, allow-ipv6);
    init(family => addr.family());
    open addr;
  }
  (bool got, float value) myTimeout;
  /*Function: setTimeout
  Sets a timeout for socket read/write operations (in seconds)*/
  void setTimeout(float t) {
    // DO NOT USE SETSOCKOPT HERE !!!!!!!!!
    myTimeout = (true, t);
  }
  void delegate(AsyncBlockMode mode) asyncfun;
  /*Function: setAsyncFun
  Implements AsyncHandle.
  If set, recv/send will retry as long as they cannot send
  or receive any data. However, they will also alternatingly call the
  yield() function passed as a parameter.*/
  override void setAsyncFun(void delegate(AsyncBlockMode mode) yield) {
    asyncfun = yield;
    
    platform(posix) {
      import c.fcntl;
      auto flags = sockfd.fcntl (F_GETFL);
      if (yield) {
        flags |= O_NONBLOCK;
      } else {
        flags &= int: Â¬O_NONBLOCK;
      }
      sockfd.fcntl(F_SETFL, flags);
    }
    platform(*-mingw32) {
      assert(false, "TODO find win32 replacement for fcntl");
    }
  }
  /*Function: reuse
  Marks the socket as reusable*/
  void reuse(bool b) {
    platform(*-mingw32) {
      setsockopt(sockfd, SOL_SOCKET, SO_REUSEADDR, char*:&b, size-of bool);
    }
    platform(posix) {
      setsockopt(sockfd, SOL_SOCKET, SO_REUSEADDR, &b, size-of bool);
    }
  }
  /*Function: linger
  On close(), linger up to t seconds or until all data has been transmitted.*/
  void linger(bool dolinger, int t) {
    c_socket_linger l;
    l.l_onoff = short:int: dolinger;
    l.l_linger = short: t;
    platform(*-mingw32) {
      setsockopt(sockfd, SOL_SOCKET, SO_LINGER, char*: &l, size-of c_socket_linger);
    }
    platform(!*-mingw32) {
      setsockopt(sockfd, SOL_SOCKET, SO_LINGER, &l, size-of c_socket_linger);
    }
  }
  // alias isOpen = sockfd;
  /*Function: open*/
  void open(Address addr) {
    platform(posix) {
      auto res = c.sys.socket.connect (sockfd, addr.getAddrHandle());
      if (res == -1) raise new std.file.ErrnoError();
    }
    platform(*-mingw32) {
      auto res = c.winsock2.connect (sockfd, addr.getAddrHandle());
    }
  }
  /*Function: recv
  Fills the argument with data from the socket, returning the number of bytes received. */
  int recv(ubyte[] buf) {
    if (myTimeout.got) {
      SelectSet ss;
      ss.add(this, AsyncBlockMode.Reading);
      ss.select(int:(myTimeout.value*1_000_000));
      if (!ss.isReady(this, AsyncBlockMode.Reading))
        raise new SocketError "read timed out";
    }
    while (true) {
      platform(posix) {
        auto res = .recv(sockfd, buf.ptr, buf.length, 0);
      }
      platform(*-mingw32) {
        auto res = .recv(sockfd, char*:buf.ptr, buf.length, 0);
      }
      if (asyncfun && res == -1 && (errno == EAGAIN || errno == EWOULDBLOCK)) {
        // yield? in any case, retry
        asyncfun?(AsyncBlockMode.Reading);
        continue;
      }
      if (res <= 0) {
        close;
      }
      return res;
    }
  }
  /*Function: send
  Sends some of the argument, returning the number of bytes sent. */
  int send(ubyte[] buf) {
    while (true) {
      platform(posix) {
        auto res = .send(sockfd, buf.ptr, buf.length, 0);
      }
      platform(*-mingw32) {
        auto res = .send(sockfd, char*:buf.ptr, buf.length, 0);
      }
      if (res == -1 && (errno == EAGAIN || errno == EWOULDBLOCK)) {
        // yield? in any case, retry
        asyncfun?(AsyncBlockMode.Writing);
        continue;
      }
      if (res <= 0) {
        close;
      }
      return res;
    }
  }
  /*Function: sendAll
  Sends all of the argument*/
  void sendAll(ubyte[] buf) {
    while buf.length {
      auto res = send buf;
      if (res <= 0) return;
      buf = buf[res .. $];
    }
  }
  /*Function: bind
  Bind the socket to an address*/
  void bind(Address addr) {
    boundAddr = addr;
    auto err = .bind(sockfd, addr.getAddrHandle());
    if (err == -1)
      raise new Error "While binding to $addr: $(CToString strerror errno)";
  }
  /*Function: listen
  Listen for incoming connections */
  void listen(int backlog = 4) {
    auto err = .listen(sockfd, backlog);
    if (err == -1)
      raise new Error "While trying to listen: $(CToString strerror errno)";
  }
  /*Function: accept
  Once incoming connections occur, return a socket for them*/
  Socket accept() {
    using new Socket (=>bare) {
      that.boundAddr = this.boundAddr.dup;
      auto hdl = that.boundAddr.getAddrHandle();
      int gotLength = hdl[1];
      auto fam = hdl[0].sa_family;
      while (true) {
        auto res = .accept(this.sockfd, hdl[0], &gotLength);
        if (res == -1 && (errno == EAGAIN || errno == EWOULDBLOCK)) {
          this.asyncfun?(AsyncBlockMode.Reading);
          continue;
        }
        if (res == -1) {
          raise new Error "While accepting connections on $(this.sockfd): $(CToString strerror errno)";
        }
        if (hdl[0].sa_family != fam || gotLength > hdl[1])
          raise new Error ("Accepted socket address was of incompatible type to listening socket: "
            ~"$(hdl[0].sa_family) ($gotLength), but expected $(fam) ($(hdl[1]))! ");
        that.sockfd = res;
        return that;
      }
    }
  }
  Address getRemoteAddress() {
    sockaddr_in address;
    if (getpeername(sockfd, sockaddr*:&address, &int result-size = size-of sockaddr_in) == -1) {
      raise new std.file.ErrnoError;
    }
    auto res = new Address;
    res.saddr = (ubyte[]:(&address)[0..1]).dup;
    return res;
  }
}

class sockreader : Iterator!ubyte[] {
  Socket sock;
  bool done;
  ubyte x 4096  buf;
  void init(Socket s) sock = s;
  bool advance() {
    auto size = sock.recv buf[];
    if size == -1 raise new SocketError errno;
    if size == 0 return false;
    value = buf[0 .. size];
    return true;
  }
}

sockreader readsocket(Socket s) return new sockreader s;
