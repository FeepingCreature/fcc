module std.lib.sdl_base;

public import c.SDL.SDL;
import std.string, std.lib.opengl.window;

platform(*-mingw32) {
  pragma(lib, "winmm");
  pragma(lib, "gdi32");
}
pragma(lib, "SDL");

class Surface {
  SDL_Surface* back;
  RefCounted rc;
  void free() { SDL_FreeSurface back; }
  alias release = rc.release();
  alias claim = rc.claim();
  alias w = back.w;
  alias h = back.h;
  void flip() SDL_Flip back;
  void init() {
    rc.onZero = &free;
  }
  void init(SDL_Surface* surf) {
    back = surf;
    init();
  }
}

shared bool[] keyPressed, keyPushed;
void init() {
  SDL_Init SDL_INIT_VIDEO;
  keyPressed = new bool[] 1K;
  keyPushed = new bool[] 1K;
}

shared vec2i mouse-pos;

shared bool mouse-clicked, mouse-pressed, mouse-released;

shared void delegate(int, int) onResize;

// pragma(msg, string-of type-of (*SDL_Event*: null).resize.w);

import std.macros.switchover;
void update() {
  (mouse-clicked, mouse-released) = false x 2;
  for int i <- 0..keyPushed.length
    keyPushed[i] = false;
  
  while SDL_PollEvent &SDL_Event ev using ev switch int t over t == type {
    case SDL_QUIT: raise new SDLQuit;
    case SDL_ACTIVEEVENT: // TODO callback
    case SDL_VIDEOEXPOSE: // TODO callback
    case SDL_KEYDOWN: using key.keysym {
      if (sym < keyPressed.length) { keyPressed[sym] = true; keyPushed[sym] = true; }
    }
    case SDL_KEYUP: using key.keysym {
      if (sym < keyPressed.length) { keyPressed[sym] = false; }
    }
    case SDL_MOUSEMOTION: mouse-pos = motion.(vec2i(x, y));
    case SDL_MOUSEBUTTONDOWN: mouse-clicked = true; mouse-pressed = true;
    case SDL_MOUSEBUTTONUP: mouse-released = true; mouse-pressed = false;
    case SDL_VIDEORESIZE: using resize if (onResize) { onResize(w, h); }
    default: writeln "| Unknown event: $$short:type";
  }
}

class SDL-Error : Error {
  void init(string fun, int res) {
    super.init "$fun: $res: $(CToString SDL_GetError())";
  }
}

class SDLQuit : WindowClosed {
  void init() { super.init "SDL_QUIT"; }
}
